buildscript {
    ext.kotlin_version = '1.9.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def getNamespaceForProject(String projectName) {
    switch (projectName) {
        case 'gallery_saver':
            return "carnegietechnologies.gallery_saver"
        case 'flutter_plugin_android_lifecycle':
            return "io.flutter.plugins.flutter_plugin_android_lifecycle"
        case 'image_picker_android':
            return "io.flutter.plugins.imagepicker"
        case 'path_provider_android':
            return "io.flutter.plugins.pathprovider"
        default:
            return "com.wigiart.birthdayapp"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                namespace = getNamespaceForProject(project.name)
                
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = '17'
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
